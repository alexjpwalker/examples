define

# Attributes

name sub attribute,
	datatype string;
has-required-techs sub attribute,
	datatype boolean;
has-required-resources sub attribute,
	datatype boolean;
can-begin sub attribute,
	datatype boolean;
can-proceed sub attribute,
	datatype boolean;
started sub attribute,
	datatype boolean;
progress sub attribute,
	datatype double;
completed sub attribute,
	datatype boolean;
quantity sub attribute,
	datatype long;
quantity-consumed sub attribute,
	datatype long;

# Static entities

research-project sub entity,
	has name,
	plays research-task,
	plays required-tech,
	plays research-to-begin;

item sub entity,
	has name,
	plays owned-item,
	plays consumes-resource;

# Dynamic entities

campaign sub entity,
	has name,
	plays campaign-with-tasks,
	plays item-owner;

# Static relations

requirement sub relation,
	abstract,
	relates prerequisite,
	relates outcome,
	plays task;

tech-requirement sub requirement,
	abstract,
	relates required-tech as prerequisite,
	relates outcome;

tech-requirement-to-begin-task sub tech-requirement,
	abstract,
	relates required-tech as prerequisite,
	relates task-to-begin as outcome;

tech-requirement-to-begin-research sub tech-requirement-to-begin-task,
	relates required-tech as prerequisite,
	relates research-to-begin as outcome;

resource-cost sub requirement,
	abstract,
	relates consumes-resource as prerequisite,
	relates outcome,
	has quantity-consumed;

resource-cost-to-begin-task sub resource-cost,
	abstract,
	relates consumes-resource as prerequisite,
	relates task-to-begin as outcome;

resource-cost-to-begin-research sub resource-cost-to-begin-task,
	relates consumes-resource as prerequisite,
	relates research-to-begin as outcome;

# Dynamic relations

campaign-task sub relation,
	abstract,
	relates campaign-with-tasks,
	relates task,
	has has-required-techs,
	has has-required-resources,
	has can-begin,
	has started,
	has can-proceed,
	has progress,
	has completed;

campaign-research-task sub campaign-task,
	relates campaign-with-tasks,
	relates research-task as task;

item-ownership sub relation,
	relates item-owner,
	relates owned-item,
	has quantity;

# Test data (for visualization purposes, move out of this file later)

# insert

# # Static data
# $sectoid_autopsy isa research-project, has name "Sectoid Autopsy";
# $alien_biotech isa research-project, has name "Alien Biotech";
# $sectoid_corpse isa item, has name "Sectoid Corpse";
# (research-to-begin: $sectoid_autopsy, required-tech: $alien_biotech) isa tech-requirement-to-begin-research;
# (research-to-begin: $sectoid_autopsy, consumes-resource: $sectoid_corpse) isa resource-cost-to-begin-research, has quantity-consumed 1;

# # Dynamic data
# $campaign1 isa campaign, has name "Campaign 1";
# (campaign-with-tasks: $campaign1, research-task: $alien_biotech) isa campaign-research-task, has progress 0;
# (campaign-with-tasks: $campaign1, research-task: $sectoid_autopsy) isa campaign-research-task, has progress 0;
# (item-owner: $campaign1, owned-item: $sectoid_corpse) isa item-ownership, has quantity 0;

# Rules

## campaign task
task-is-not-completed-if-progress-is-less-than-100 sub rule,
when {
	$task isa campaign-task, has progress < 100;
}, then {
	$task has completed false;
};

task-is-completed-if-progress-is-at-least-100 sub rule,
when {
	$task isa campaign-task, has progress >= 100;
}, then {
	$task has completed true;
};

task-can-not-begin-if-it-is-already-started sub rule,
when {
	$task isa campaign-task, has started true;
}, then {
	$task has can-begin false;
};

task-can-not-begin-if-it-lacks-required-tech sub rule,
when {
	$task isa campaign-task, has has-required-techs false;
}, then {
	$task has can-begin false;
};

task-can-not-begin-if-it-lacks-required-resources sub rule,
when {
	$task isa campaign-task, has has-required-resources false;
}, then {
	$task has can-begin false;
};

task-can-begin-if-it-is-not-started-and-all-requirements-are-met sub rule,
when {
	$task isa campaign-task, has started false, has has-required-techs true, has has-required-resources true;
}, then {
	$task has can-begin true;
};

research-does-not-have-required-techs sub rule,
when {
	$campaign isa campaign;
	$campaign_task($campaign, task: $task) isa campaign-task;
	(research-to-begin: $task, required-tech: $prerequisite) isa tech-requirement-to-begin-research;
	($campaign, $prerequisite) isa campaign-task, has completed false;
}, then {
	$campaign_task has has-required-techs false;
};

research-has-required-techs sub rule,
when {
	$campaign isa campaign;
	$campaign_task($campaign, task: $task) isa campaign-task;
	not {
		(research-to-begin: $task, required-tech: $prerequisite) isa tech-requirement-to-begin-research;
		($campaign, $prerequisite) isa campaign-task, has completed false;
	};
}, then {
	$campaign_task has has-required-techs true;
};

research-does-not-have-required-resources sub rule,
when {
	$campaign isa campaign;
	$campaign_task($campaign, task: $task) isa campaign-task;
	(research-to-begin: $task, consumes-resource: $resource) isa resource-cost-to-begin-research, has quantity-consumed $quantity_required;
	($campaign, $resource) isa item-ownership, has quantity < $quantity_required;
}, then {
	$campaign_task has has-required-resources false;
};

research-does-have-required-resources sub rule,
when {
	$campaign isa campaign;
	$campaign_task($campaign, task: $task) isa campaign-task;
	not {
		(research-to-begin: $task, consumes-resource: $resource) isa resource-cost-to-begin-research, has quantity-consumed $quantity_required;
		($campaign, $resource) isa item-ownership, has quantity < $quantity_required;
	};
}, then {
	$campaign_task has has-required-resources true;
};

task-can-not-proceed-if-it-is-not-started sub rule,
when {
	$task isa campaign-task, has started false;
}, then {
	$task has can-proceed false;
};

task-can-not-proceed-if-it-is-completed sub rule,
when {
	$task isa campaign-task, has completed true;
}, then {
	$task has can-proceed false;
};

task-can-proceed-if-it-is-started-but-not-completed sub rule,
when {
	$task isa campaign-task, has started true, has completed false;
}, then {
	$task has can-proceed true;
};

task-has-no-progress-if-it-has-not-been-started sub rule,
when {
	$task isa campaign-task, has started false;
}, then {
	$task has progress 0;
};

# Queries (for visualization; remove later)

# Fetch all research projects currently available to work on, and their current progress % in Campaign 1
# match
# 	$campaign isa campaign, has name = "Campaign 1";
# 	$research_project isa research-project;
# 	($campaign, research-task: $research_project) isa campaign-research-task, has can-proceed true, has progress $progress;
# get $project, $research_project;
